#+ summary: Get the nanopublications referring to a given paper.
#+ method: GET
#+ pagination: 1000
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix oa: <http://www.w3.org/ns/oa#>

select distinct ?np ?user ?type ?subj ?subjLabel ?rel ?relLabel ?text ?selection ?date where {
  graph npa:graph {
    ?np npa:hasHeadGraph ?h .
    ?np dct:created ?date .
    ?np npa:hasValidSignatureForPublicKey ?pubkey .
  }
  graph ?h {
    ?np np:hasAssertion ?a .
    ?np np:hasPublicationInfo ?i.
  }
  graph ?a {
    ?subj ?rel ?_paper_iri .
  }
  optional { ?a rdfs:comment ?textPre . bind(str(?textPre) as ?text) }
  optional { graph <http://purl.org/np/RA6d0qLmlRdXd-ubre83LxP22auJdJjXGzaW3W3qH4Neo#assertion> {
    ?rel rdfs:label ?relLabelPre .
    bind("cito" as ?typePre)
  } }
  optional { graph ?a {
    ?annotation oa:hasTarget ?annotationtarget ;
      oa:hasBody ?annotationbody .
    ?annotationbody rdf:value ?annotationtext .
    ?annotationtarget oa:hasSource ?_paper_iri ;
      oa:hasSelector ?annotationSelector .
    optional { ?annotationSelector oa:prefix ?annotatedprefix . }
    ?annotationSelector oa:exact ?annotatedtext .
    optional { ?annotationSelector oa:suffix ?annotatedsuffix . }
    bind("oa" as ?typePre)
    bind(str(?annotationtext) as ?text)
    bind(concat(?annotatedprefix, "<h>", ?annotatedtext, "</h>", ?annotatedsuffix) as ?selection)
  } }
  bind(replace(str(?subj), '^.*?([a-zA-Z0-9\\-\\_]+)$', '$1') as ?subjLabel).
  bind(coalesce(str(?relLabelPre), replace(str(?rel), '^.*?([a-zA-Z0-9\\-\\_]+)$', '$1')) as ?relLabel).
  bind(coalesce(?typePre, "other") as ?type).
  graph ?i {
    ?np dct:creator ?user .
  }
  filter not exists {
    graph npa:graph {
      ?newversion npa:hasHeadGraph ?nh .
      ?newversion npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?nh {
      ?newversion np:hasPublicationInfo ?ni .
    }
    graph ?ni {
      ?newversion npx:supersedes ?np .
    }
  }
  filter not exists {
    graph npa:graph {
      ?retraction npa:hasHeadGraph ?rh .
      ?retraction npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?rh {
      ?retraction np:hasAssertion ?ra .
    }
    graph ?ra {
      ?person npx:retracts ?np .
    }
  }
}
order by desc(?date)
