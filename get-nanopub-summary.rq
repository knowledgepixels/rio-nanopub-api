#+ summary: Get the nanopublications referring to a given paper.
#+ method: GET
#+ pagination: 1000
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix oa: <http://www.w3.org/ns/oa#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select distinct ?np ?user (count(?subj) as ?assertionTriples) (sample(?subj) as ?subjSample) (sample(?rel) as ?relSample) (sample(?obj) as ?objSample) ?thing ?thingLabel ?date where {
  graph npa:graph {
    ?_np_iri npa:hasHeadGraph ?h .
    ?_np_iri dct:created ?date .
    ?_np_iri npa:hasValidSignatureForPublicKey ?pubkey .
  }
  graph ?h {
    ?_np_iri np:hasAssertion ?a .
    ?_np_iri np:hasPublicationInfo ?i .
  }
  graph ?a {
    ?subj ?rel ?obj .
    optional { ?thing rdfs:label ?thingLabel. }
  }
  graph ?i {
    ?_np_iri dct:creator ?user .
    optional { ?_np_iri npx:introduces ?thing . }
  }
  bind(?_np_iri as ?np)
} group by ?np ?user ?thing ?thingLabel ?date
