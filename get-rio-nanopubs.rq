#+ summary: Get all nanopublications relevant to the RIO Journal
#+ method: GET
#+ pagination: 1000
#+ endpoint: https://query.knowledgepixels.com/repo/meta

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix biolink: <https://w3id.org/biolink/vocab/>
prefix nt: <https://w3id.org/np/o/ntemplate/>
prefix biodiv: <https://w3id.org/kpxl/biodiv/terms/>
prefix prov: <http://www.w3.org/ns/prov#>

select distinct ?np ?label ?pubkey ?date where {
  graph npa:graph {
    ?np npa:hasValidSignatureForPublicKey ?pubkey .
    ?np rdfs:label ?label .
    values ?type {
      <https://w3id.org/kpxl/rel/terms/hasRelationTo>
      <https://w3id.org/kpxl/rel/terms/tendsToHaveRelationTo>
      <https://w3id.org/linkflows/superpattern/terms/SuperPatternInstance>
      <http://purl.org/petapico/o/hycl#AIDA-Sentence>
      <https://w3id.org/biolink/vocab/related_to>
      <http://www.w3.org/2004/02/skos/core#relatedMatch>
    }
    ?np npx:hasNanopubType ?type .
    ?np dct:creator ?__creator_iri .
    ?np dct:created ?date .
    filter not exists { ?np npx:hasNanopubType npx:ExampleNanopub . }
    filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }
  }
}
order by desc(?date)
